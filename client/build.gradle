/*
 * Copyright OpenSearch Contributors
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
    id "io.freefair.lombok" version "8.14"
}

apply plugin: 'opensearch.build'
apply plugin: "com.diffplug.spotless"

group = opensearch_group
version = "${opensearch_build}"
description = 'OpenSearch Geospatial client'

project.loggerUsageCheck.enabled = false
project.testingConventions.enabled = false
project.forbiddenApisTest.enabled = false

repositories {
    mavenLocal()
    maven { url "https://central.sonatype.com/repository/maven-snapshots/" }
    maven { url "https://aws.oss.sonatype.org/content/repositories/snapshots" }
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
}

compileJava {
    options.compilerArgs.addAll(["-processor", 'lombok.launch.AnnotationProcessorHider$AnnotationProcessor'])
}
compileTestJava {
    options.compilerArgs.addAll(["-processor", 'lombok.launch.AnnotationProcessorHider$AnnotationProcessor'])
}


dependencies {
    compileOnly "${group}:opensearch:${opensearch_version}"

    testImplementation "junit:junit:${versions.junit}"
    testImplementation "org.mockito:mockito-core:${versions.mockito}"
    testImplementation "org.hamcrest:hamcrest:${versions.hamcrest}"
    testImplementation "net.bytebuddy:byte-buddy:${versions.bytebuddy}"
    testImplementation "net.bytebuddy:byte-buddy-agent:${versions.bytebuddy}"
}

licenseFile = "LICENSE.txt"
noticeFile = "NOTICE.txt"

spotless {
    java {
        removeUnusedImports()
        importOrder 'java', 'javax', 'org', 'com'
        eclipse().configFile rootProject.file('formatterConfig.xml')
        trimTrailingWhitespace()
        endWithNewline()
    }
}

publishing {
    repositories {
        maven {
            name = 'staging'
            url = "${rootProject.buildDir}/local-staging-repo"
        }
        maven {
            name = "Snapshots"
            url = "https://central.sonatype.com/repository/maven-snapshots/"
            credentials {
                username System.getenv("SONATYPE_USERNAME")
                password System.getenv("SONATYPE_PASSWORD")
            }
        }
    }
    publications {
        all {
            pom {
                name = "opensearch-geospatial-client"
                description = 'OpenSearch Geospatial client'
            }
            pom.withXml { XmlProvider xml ->
                Node node = xml.asNode()
                node.appendNode('inceptionYear', '2021')

                Node license = node.appendNode('licenses').appendNode('license')
                license.appendNode('name',  "The Apache License, Version 2.0")
                license.appendNode('url', "http://www.apache.org/licenses/LICENSE-2.0.txt")

                Node developer = node.appendNode('developers').appendNode('developer')
                developer.appendNode('name', 'OpenSearch')
                developer.appendNode('url', 'https://github.com/opensearch-project/geospatial')
            }
        }
    }
}


